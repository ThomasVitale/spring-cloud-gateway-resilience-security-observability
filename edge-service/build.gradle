plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.thomasvitale'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
        nativeImageCapable = true
    }
}

repositories {
    mavenCentral()
}

ext {
    set('arconiaVersion', "0.10.0")
    set('springCloudVersion', "2024.0.1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.arconia:arconia-opentelemetry-spring-boot-starter'

    // Gateway
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

    // Circuit Breaker
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
    implementation 'io.github.resilience4j:resilience4j-micrometer'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.session:spring-session-data-redis'

    // Only on Apple Silicon. Why it's necessary: https://github.com/netty/netty/issues/11020
    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.119.Final:osx-aarch_64'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testAndDevelopmentOnly 'io.arconia:arconia-dev-services-opentelemetry-lgtm'
    testAndDevelopmentOnly 'io.arconia:arconia-dev-services-redis'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "io.arconia:arconia-bom:${arconiaVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootBuildImage') {
    builder = "docker.io/paketobuildpacks/builder-noble-java-tiny"
    imageName = "${project.name}:main"
}
